plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'io.freefair.lombok' version '8.4'
    id "org.jsonschema2pojo" version "1.2.1"
    id 'com.google.protobuf' version '0.9.4'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

group = 'com.java-template'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/source/proto/main/java"
            srcDirs += "$buildDir/generated/source/proto/main/grpc"
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.2.4'
    implementation 'org.springframework:spring-remoting:2.0.8'
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'com.google.protobuf:protobuf-java:3.25.1'
    implementation 'io.grpc:grpc-protobuf:1.62.2'
    implementation 'io.grpc:grpc-core:1.62.2'
    implementation 'io.grpc:grpc-stub:1.62.2'

    implementation 'io.grpc:grpc-all:1.62.2'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'io.cloudevents:cloudevents-protobuf:3.0.0'
    implementation 'io.cloudevents:cloudevents-json-jackson:3.0.0'
    implementation 'io.cloudevents:cloudevents-spring:3.0.0'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation 'io.github.cdimascio:java-dotenv:5.2.2'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'com.fasterxml.uuid:java-uuid-generator:4.0.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

test {
    useJUnitPlatform()
}

jsonSchema2Pojo {
    targetPackage = 'org.cyoda.cloud.api.event'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.1'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.62.2'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

["bootRun", "build", "compileJava"].each { taskName ->
    tasks.named(taskName).configure { dependsOn("generateProto") }
}

bootJar {
    mainClass.set("com.java_template.Application")
}

task bootJarPrototype(type: org.springframework.boot.gradle.tasks.bundling.BootJar) {
    group = 'build'
    description = 'Boot JAR for Prototype'
    archiveClassifier.set("prototype")
    mainClass.set("com.java_template.Prototype")
    destinationDirectory.set(layout.buildDirectory.dir("libs"))
    launchScript()
    classpath = sourceSets.main.runtimeClasspath
    mainClassName = "com.java_template.Prototype"
    targetJavaVersion = JavaVersion.VERSION_21
    dependsOn configurations.runtimeClasspath
}

task bootJarWorkflowImport(type: org.springframework.boot.gradle.tasks.bundling.BootJar) {
    group = 'build'
    description = 'Boot JAR for Workflow Import Tool'
    archiveClassifier.set("workflow-import")
    mainClass.set("com.java_template.common.tool.WorkflowImportTool")
    destinationDirectory.set(layout.buildDirectory.dir("libs"))
    launchScript()
    classpath = sourceSets.main.runtimeClasspath
    mainClassName = "com.java_template.common.tool.WorkflowImportTool"
    targetJavaVersion = JavaVersion.VERSION_21
    dependsOn configurations.runtimeClasspath
}

tasks.register('runApp', JavaExec) {
    group = 'application'
    description = 'Run arbitrary main class'
    classpath = sourceSets.main.runtimeClasspath
    def requestedMain = project.findProperty('mainClass') ?: 'com.java_template.Application'
    mainClass.set(requestedMain.toString())
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(21))
    })
}
